; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Caurix Template Operator"
#define MyAppVersion "1.0"
#define MyAppPublisher "de3452"
#define MyAppExeName "CaurixTemplateOperator.exe"
#define MyAppId "{61B16427-33FD-46CC-9A5B-B26BB642395D}"

[CustomMessages]
english.NewerVersionExists=A newer version of {#MyAppName} is already installed.%n%nInstaller version: {#MyAppVersion}%nCurrent version: 
[Code]
// find current version before installation
function InitializeSetup: Boolean;
var Version: String;
begin
  if RegValueExists(HKEY_LOCAL_MACHINE,'Software\Microsoft\Windows\CurrentVersion\Uninstall\{#MyAppId}_is1', 'DisplayVersion') then
    begin
      RegQueryStringValue(HKEY_LOCAL_MACHINE,'Software\Microsoft\Windows\CurrentVersion\Uninstall\{#MyAppId}_is1', 'DisplayVersion', Version);
      if Version > '{#MyAppVersion}' then
        begin
          MsgBox(ExpandConstant('{cm:NewerVersionExists} '+Version), mbInformation, MB_OK);
          Result := False;
        end
      else
        begin
          Result := True;
        end
    end
  else
    begin
      Result := True;
    end
end;

function IsDotNetDetected(version: string; service: cardinal): boolean;
// http://www.kynosarges.de/DotNetVersion.html
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1'          .NET Framework 1.1
//    'v2.0'          .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//    'v4.5'          .NET Framework 4.5
//    'v4.5.1'        .NET Framework 4.5.1
//    'v4.5.2'        .NET Framework 4.5.2
//    'v4.6'          .NET Framework 4.6
//    'v4.6.1'        .NET Framework 4.6.1
//    'v4.6.2'        .NET Framework 4.6.2
//    'v4.7'          .NET Framework 4.7
//    'v4.7.1'        .NET Framework 4.7.1
//    'v4.7.2'        .NET Framework 4.7.2
//    'v4.8'          .NET Framework 4.8
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key, versionKey: string;
    install, release, serviceCount, versionRelease: cardinal;
    success: boolean;
begin
    versionKey := version;
    versionRelease := 0;

    // .NET 1.1 and 2.0 embed release number in version key
    if version = 'v1.1' then begin
        versionKey := 'v1.1.4322';
    end else if version = 'v2.0' then begin
        versionKey := 'v2.0.50727';
    end

    // .NET 4.5 and newer install as update to .NET 4.0 Full
    else if Pos('v4.', version) = 1 then begin
        versionKey := 'v4\Full';
        case version of
          'v4.5':   versionRelease := 378389;
          'v4.5.1': versionRelease := 378675; // 378758 on Windows 8 and older
          'v4.5.2': versionRelease := 379893;
          'v4.6':   versionRelease := 393295; // 393297 on Windows 8.1 and older
          'v4.6.1': versionRelease := 394254; // 394271 before Win10 November Update
          'v4.6.2': versionRelease := 394802; // 394806 before Win10 Anniversary Update
          'v4.7':   versionRelease := 460798; // 460805 before Win10 Creators Update
          'v4.7.1': versionRelease := 461308; // 461310 before Win10 Fall Creators Update
          'v4.7.2': versionRelease := 461808; // 461814 before Win10 April 2018 Update
          'v4.8':   versionRelease := 528040; // 528049 before Win10 May 2019 Update
        end;
    end;

    // installation key group for all .NET versions
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + versionKey;

    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;

    // .NET 4.0 and newer use value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;

    // .NET 4.5 and newer use additional value Release
    if versionRelease > 0 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Release', release);
        success := success and (release >= versionRelease);
    end;

    result := success and (install = 1) and (serviceCount >= service);
end;

function CheckDotNet() : boolean;
begin
    result := not IsDotNetDetected('v4.5',0);
end;

const    
  //mysqlodbc_reg = 'SOFTWARE\ODBC\ODBCINST.INI\';
  mysqlodbc_reg = 'SOFTWARE\MySQL AB\';
type
    MySQLODBCType = (MySQL351, MySQL501, MySQL53);

function mysqlodbcinstalled(odbcversion: MySQLODBCType): boolean;
var
  MyResult: boolean;
begin
    MyResult:= False;
    case odbcversion of            
      MySQL351:
                if RegKeyExists(HKLM, mysqlodbc_reg + 'MySQL Connector/ODBC 3.51') then
        begin
          MyResult := True;
        end;
      MySQL501:
                if RegKeyExists(HKLM, mysqlodbc_reg + 'MySQL Connector/ODBC 5.01') then
        begin
          MyResult := True;
        end;      
      MySQL53:
        if RegKeyExists(HKLM, mysqlodbc_reg + 'MySQL Connector/ODBC 5.3') then
        begin
          MyResult := True;
        end; 
    end;
    Result := MyResult;
end;

function CheckOdbc(): boolean;
begin
    result := not mysqlodbcinstalled(MySQL53);
end;



[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{61B16427-33FD-46CC-9A5B-B26BB642395D}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputBaseFilename=mysetup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
PrivilegesRequired=admin 

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\CaurixTemplateOperator.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\BouncyCastle.Crypto.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\CaurixTemplateOperator.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\CaurixTemplateOperator.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\CaurixTemplateOperator.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\itextsharp.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\itextsharp.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\log.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\MailKit.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\MailKit.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\MimeKit.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\MimeKit.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\MySql.Data.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\NetOffice.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\NetOffice.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\Newtonsoft.Json.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\Newtonsoft.Json.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\OfficeApi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\OfficeApi.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\OutlookApi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\OutlookApi.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\stdole.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\VBIDEApi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\VBIDEApi.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\Template.docx"; DestDir: "{app}"; Flags: ignoreversion
;Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\updOutlookRegValues\bin\Release\updOutlookRegValues.exe"; DestDir: "{app}"; Flags: ignoreversion
;Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\updOutlookRegValues\bin\Release\updOutlookRegValues.exe.config"; DestDir: "{app}"; Flags: ignoreversion
;Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\updOutlookRegValues\bin\Release\updOutlookRegValues.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\NDP461-KB3102438-Web.exe"; DestDir: "{app}\Lib"; Flags: deleteafterinstall  
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\mysql-connector-odbc-5.3.14-win32.msi"; DestDir: "{app}\Lib"; Flags: deleteafterinstall
Source: "C:\Users\Demi\source\repos\CaurixTemplateOperator\CaurixTemplateOperator\bin\Release\mysql-connector-odbc-5.3.14-winx64.msi"; DestDir: "{app}\Lib"; Flags: deleteafterinstall
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\Lib\mysql-connector-odbc-5.3.14-win32.msi"; Description: "Updates for ODBC Driver"; StatusMsg: "Updating ODBC Driver"; Flags: shellexec waituntilterminated; Check: CheckOdbc    
;Filename: "{app}\Lib\mysql-connector-odbc-5.3.14-winx64.msi"; Description: "Updates for ODBC Driver"; StatusMsg: "Updating ODBC Driver"; Flags: shellexec waituntilterminated; Check: IsWin64
Filename: "{app}\Lib\NDP461-KB3102438-Web.exe"; Description: "Updates for .Net Framework"; StatusMsg: "Updating .Net Framework"; Flags: waituntilterminated; Check: CheckDotNet
; Filename: "{app}\updOutlookRegValues.exe"; Description: "Registry update for Outlook"; StatusMsg: "Updating Outlook settings"; Flags: waituntilterminated
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
